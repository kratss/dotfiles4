#!/usr/bin/env fish 
# FIsh FIle Gui handler
# fifig opens files in more traditional GUI applications than fifi
function get_special
    # Special categories like audiobook, which have the same extensions
    # as music files, but are handled differently

    # If it is an audio file in a folder containing the word audiobook,
    # use audiobook handler
    if string match -iq '*audiobook*' $file
        and string match -q 'audio/*' "$type"
        handle_audiobook
    else if string match -iq 'http*' $file
        handle_web
    end
end
function get_mime
    switch $type
        case application/pdf
            handle_pdf
        case application/vnd.oasis.opendocument.text
            handle_opendoctext
        case 'image/*'
            imv-dir 2>/dev/null $file
        case 'audio/*'
            handle_audio
        case text/calendar
            handle_calendar
        case 'text/*'
            handle_text
            exit
    end
end
function get_ext
    set ext (path extension "$file")
    switch $ext
        case ""
            return
        case .md
            handle_markdown
        case .pdf
            handle_pdf
        case .m4b
            handle_audiobook
        case .html
            handle_web
        case .ics
            handle_calendar
        case .epub
            handle_ebook
    end
end
function handle_audio
    mpv $file 2>/dev/null &
    exit
end
function handle_audiobook
    $DEFAULT_TERM mpv --save-position-on-quit $file 2>/dev/null &
    exit
end
function handle_ebook
    # --command is required. not sure why
    flatpak run --command=foliate com.github.johnfactotum.Foliate $file
    exit
end
function handle_calendar
    flatpak run org.gnome.Calendar $file &
    exit
end
function handle_markdown
    flatpak run com.github.fabiocolacio.marker $file 2>/dev/null &
    exit
end
function handle_opendoctext
    libreoffice $file
end
function handle_pdf
    flatpak run org.gnome.Evince $file 2>/dev/null &
    exit
end
function handle_text
    $DEFAULT_TERM $EDITOR $file 2>/dev/null &
    exit
end
function handle_web
    qutebrowser-fast $file 2>/dev/null &
    exit
end
argparse -- $argv
or return
set file $argv
set type (file --mime-type -b $file)
get_special
get_ext
get_mime
/usr/bin/xdg-open $file
exit
